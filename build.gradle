plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'dev.tnitan'
version '1.0.0-SNAPSHOT'

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        library(MavenPublication) {
            artifactId = 'imageproxy-sdk'
            from components.java
            pom {
                name = 'Imgproxy SDK'
                description = 'Signed URLs generator for Imgproxy'
                url = 'https://github.com/tnitan/imgproxysdk'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'tnitan'
                        name = 'Tiberiu Nitan'
                        email = 'contact@tnitan.dev'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:tnitan/imgproxysdk.git'
                    developerConnection = 'scm:git:ssh://github.com:tnitan/imgproxysdk.git'
                    url = 'https://github.com/tnitan/imgproxysdk'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
            // url = uri("${buildDir}/publishing-repository")
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // StringUtils, ArrayUtils
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    // HmacUtils
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    // Lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation group: 'one.util', name: 'streamex', version: '0.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

signing {
    sign publishing.publications.library
}
